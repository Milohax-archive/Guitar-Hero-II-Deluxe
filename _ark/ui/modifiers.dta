{new SliderPanel modifiers_panel
   (file
      {if_else $game80s
         game_modifiers_80s.milo
         game_modifiers.milo})
   (focus gs_guitar.sld)
   (focus_component "")
   (reset_focus TRUE)
   (orig_vol 0)
   (finish_load
      {gs_guitar.sld set_num_steps 21}
      {if_else {'||'
      {== $modifier_exit multi_vs_selsong_screen}
      {== $modifier_exit multi_coop_selsong_screen}
      {== $modifier_exit multi_fo_selsong_screen}}
         {gs_sfx.sld set_num_steps 31}
         {gs_sfx.sld set_num_steps 31}}) ; was 41 steps, dropped to 31 when disallowing <100%
   (slider_changed {'||'
      {!=
         {options get band_volume_idx}
         {gs_guitar.sld current}}
      {!=
         {options get guitar_volume_idx}
         {gs_sfx.sld current}}})
   (enter
      {gs_guitar.sld set_current {int {+ 0.5 {/ {+ {- $trackspd 1} 0.5} 0.1}}}}
      {gs_sfx.sld set_current
         {int {+ 0.5 {/ {- $speedmod 1} 0.05}}}}
         ; {int {+ 0.5 {/ {+ {- $speedmod 1} 0.5} 0.05}}}} ; Original formula with <100% possible
      {stereo.chk set_check $focusmode}
      {$this display_speed_msg $trackspd $speedmod})
   (poll
      {gs_band.sld set_state kDisabled}
      {gs_band.sld set_showing FALSE})
   (reset_gm_title
      {gs_title.lbl set_localized_text GAME_MODIFIER})
   (display_speed_msg
      ($tspd $mspd)
      {gs_title.lbl set_localized_text {sprintf {localize speed_msg} {int {+ 0.5 {* $tspd 100}}} {int {+ 0.5 {* $mspd 100}}}}})
   (set_speeds
      {if {$this slider_changed}
         {do
            {set $trackspd {- {+ 1 {* {gs_guitar.sld current} 0.1}} 0.5}}
            {set $speedmod
               {+ 1 {* {gs_sfx.sld current} 0.05}}}
               ; {- {+ 1 {* {gs_sfx.sld current} 0.05}} 0.5}} ; Original formula with <100% possible
            {$this display_speed_msg $trackspd $speedmod}}})
   (slider_select_cancel
      ($component)
      {switch $component
         (gs_guitar.sld {$component set_current [orig_vol]})
         (gs_sfx.sld {$component set_current [orig_vol]})}
      {$this update_help_display}
      {$this set_speeds}
      {$this display_speed_msg $trackspd $speedmod}
      kDataUnhandled)
   (FOCUS_MSG
      {$this set focus_component {$new_focus name}}
      {$this update_help_display})
   (update_help_display
      {if_else {$this slider_selected}
         {helpbar set_display
            ((fret1 help_confirm)
               (fret2 help_cancel)
               (strum help_adjspd))}
         {do
            {if_else {== [focus_component] "gs_stereo.btn"}
               {helpbar set_display
                  ((fret1 help_onoff)
                     (fret2 help_back)
                     (strum help_updown))}
               {helpbar set_display
                  ((fret1 help_select)
                     (fret2 help_back)
                     (strum help_updown))}}}})
   (slider_start_msg
      ($component)
      {do
         {switch $component
            (gs_guitar.sld
               {do
                  {play_sfx button_error (volume 1) (stop TRUE)}
                  {$this set_speeds}})
            (gs_sfx.sld
               {do
                  {play_sfx button_error (volume 1) (stop TRUE)}
                  {$this set_speeds}})}})
   (SELECT_START_MSG
      {switch $component
         (gs_guitar.sld {$this set orig_vol {gs_guitar.sld current}})
         (gs_sfx.sld {$this set orig_vol {gs_sfx.sld current}})
         (gs_stereo.btn
            {stereo.chk toggle}
            {set $focusmode {stereo.chk get_check}})
         kDataUnhandled}
      {$this update_help_display}
      {$this set_speeds}
      {$this display_speed_msg $trackspd $speedmod})}
{new GHScreen modifiers_screen
   (panels meta modifiers_panel helpbar)
   (focus modifiers_panel)
   (allow_back FALSE)
   (BUTTON_DOWN_MSG
      {if {&&
      {! {modifiers_panel slider_selected}}
      {== $button kPad_Tri}}
         {$this backwards_anim}
         {synth play_sequence button_back.cue}
         {$this set_speeds}
         {if_else {exists $modifier_exit}
            {ui goto_screen $modifier_exit}
            {ui goto_screen main_screen}}}
      kDataUnhandled)}