; Chose what main menu to display based off egg screen
;main.milo - GH2DX
;main_egg0.milo - GH2DXCE
;main_egg1.milo - GH1
;main_egg2.milo - GH80SDX
;main_egg3.milo - GH3
;main_egg_final.milo - ???????
{new
   GHPanel
   main_panel
   (file
	{if_else
      $egg5
      main_egg_final.milo
		{if_else
			$egg4
			main_egg3.milo
			{if_else
				$egg3
				main_egg2.milo
				{if_else
					$egg2
					main_egg1.milo
					{if_else
						$egg1
						#ifndef _ONYX
						main_egg0.milo
						#else
						main.milo
						#endif
							{if_else
								$game80s
								main_egg2.milo
								#ifndef _MEGA
								main.milo
								#else
								main_egg0.milo
								#endif}}}}}})
   (focus main_quickspin.btn)
   (msg_last_frame 0)
   (already_entered FALSE)
   (memcard_flow_entered FALSE)
   (retain_last_focus FALSE)
   (enter
#ifndef _ONYX
      {set $main_entered TRUE}
#else
      {set $main_entered TRUE}
      {$this disable main_career.btn}
      {set $cheat_enabled_unlock_all 1}
      {campaign set_all_access 1}
      {set $disable_save 1}
      {set $disable_career 1}
#endif
      {$this reset_player_settings}
      {if
         {!
            [already_entered]}
         {synth play_sequence meta_lick}
         {$this set already_entered TRUE}}
      {game set_tutorial_running FALSE}
      {main_msg.view set_showing FALSE}
      {set
         [msg_last_frame]
         0})
   (SELECT_START_MSG
      {switch
         $component
         (main_career.btn
            {gamecfg set mode career}
            {if_else
               {>
                  {campaign num_profiles}
                  0}
               {ui goto_screen chooseprof_screen}
               {do
                  {nameprof_screen set back_screen main_screen}
                  {nameprof_screen set is_editing FALSE}
                  {nameprof_screen set profile_slot 0}
                  {nameprof_screen set next_screen sel_difficulty_screen}
                  {ui goto_screen nameprof_screen}}})
         (main_quickspin.btn
            {gamecfg set mode quickplay}
            {ui goto_screen qp_selsong_screen})
         (main_multiplayer.btn
            {ui goto_screen multi_screen})
         (main_tutorial.btn
            {ui goto_screen training_screen})
         (main_options.btn
            {ui goto_screen options_screen})})
   (poll
      {if_else
         {game is_missing_multi_controller}
         {$this disable main_multiplayer.btn}
         {$this enable main_multiplayer.btn}}
      {main_msg.view
         set_showing
         {<
            {taskmgr ui_seconds}
            {$this get msg_last_frame}}})
   (reset_player_settings
      {{game get_player_config 0}
         set_difficulty
         kDifficultyExpert}
;;;;;;;;if gh2 retail Elf
#ifdef _SHIP
      {game set_venue fest}
      {game set_song_index 0}
      {game set_character 
   	  	{if_else
			$game80s
				metalencore
				metal1}
				TRUE}
#endif
;;;;;;;;if 80's retail Elf
#ifdef _ENCO
      {game set_venue fest2}
      {game set_song_index 0}
      {game set_character 
   	  	{if_else
			$game80s
				metalencore
				metal1}
				TRUE}
#endif
;;;;;;;;if Mega Elf
#ifdef _MEGA
      {game set_venue fest}
      {game set_song_index 0}
      {game set_character 
   	  	{if_else
			$game80s
				metalencore
				metal1}
				TRUE}
#endif
;;;;;;;;if ONYX Elf
#ifdef _ONYX
      {game set_venue fest}
      {game set_song_index 0}
      {game set_character 
   	  	{if_else
			$game80s
				metalencore
				metal1}
				TRUE}
#endif
;;;;;;;;if 80's debug Elf
#ifdef 8DBUG
      {game set_venue fest2}
      {game set_song_index 0}
      {game set_character metal1 TRUE}
#endif
;;;;;;;;if gh2dbug Elf
#ifdef _2DBUG
      {game set_venue fest}
      {game set_song_index 0}
      {game set_character metal1 TRUE}
#endif
      {game set_guitar_index 1}
      {do
         ($player2_config
            {game get_player_config 1})
         {$player2_config set_character rockabill1 TRUE}
         {$player2_config set_guitar_index 0}
         {$player2_config set_difficulty kDifficultyMedium}})
   (display_cheat_msg
      ($cheat $enable)
      {mm_msg.lbl
         set_localized_text
         {sprintf
            {localize
               {if_else $enable cheat_enabled_fmt cheat_disabled_fmt}}
            {localize $cheat}}}
      {$this
         set
         msg_last_frame
         {'+'
            {taskmgr ui_seconds}
            5}})}
{new
   GHScreen
   main_screen
   (panels meta main_panel helpbar)
   (focus main_panel)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))
   (access_all
      {main_panel reset_player_settings})
   (enter
      {$this turn_off_ambient})}